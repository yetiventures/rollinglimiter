static def isSnapshot(version) {
    return version.contains("SNAPSHOT")
}

static def getRepositoryUrl(version) {
    if (isSnapshot(version)) {
        return 'https://oss.sonatype.org/content/repositories/snapshots'
    }
    return 'https://oss.sonatype.org/service/local/staging/deploy/maven2'
}

subprojects {

    ext {
        guavaVersion = '31.1-jre'
    }

    apply plugin: 'java-library'
    apply plugin: 'maven-publish'
    apply plugin: 'signing'

    sourceCompatibility = '1.17'
    targetCompatibility = '1.17'

    repositories {
        mavenCentral()
    }

    dependencies {
        compileOnly 'javax.annotation:jsr250-api:1.0'
        compileOnly 'com.github.spotbugs:spotbugs-annotations:4.7.3'
    }

    group = 'com.yvhk.rollinglimiter'
    version = '1.3.1'

    task sourcesJar(type: Jar) {
        from sourceSets.main.allSource
        archiveClassifier.set('sources')
    }

    task javadocJar(type: Jar) {
        from javadoc
        javadoc.options.addStringOption('Xdoclint:none', '-quiet')
        archiveClassifier.set('javadoc')
    }

    publishing {
        publications {
            mavenJava(MavenPublication) {
                artifactId = "ursa-${project.name}"
                from components.java
                artifact sourcesJar
                artifact javadocJar

                pom {
                    afterEvaluate {
                        name = project.description
                        description = project.description
                    }

                    url = 'https://github.com/yetiventures/com.yvhk.rollinglimiter'

                    scm {
                        connection = 'scm:git:git://github.com/yetiventures/com.yvhk.rollinglimiter.git'
                        developerConnection = 'scm:git:ssh://github.com/yetiventures/com.yvhk.rollinglimiter.git'
                        url = 'http://github.com/yetiventures/com.yvhk.rollinglimiter/tree/master'
                    }

                    licenses {
                        license {
                            name = 'MIT License'
                            url = 'https://opensource.org/licenses/MIT'
                        }
                    }

                    developers {
                        developer {
                            id = 'yetiventures'
                            name = 'Dev Yeti Ventures'
                            email = 'dev@yvhk.com'
                            organization = 'Yeti Ventures'
                            organizationUrl = 'https://yvhk.com'
                        }
                    }
                }
            }
        }
        repositories {
            maven {
                url = getRepositoryUrl(version)
                credentials {
                    username = project.hasProperty('sonatypeUsername') ? sonatypeUsername : ''
                    password = project.hasProperty('sonatypePassword') ? sonatypePassword : ''
                }
            }
        }
    }

    signing {
        required { !isSnapshot(version) }
        sign publishing.publications.mavenJava
    }
}
