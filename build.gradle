static def isSnapshot(version) {
    return version.contains("SNAPSHOT")
}

static def getRepositoryUrl(version) {
    if (isSnapshot(version)) {
        return 'https://oss.sonatype.org/content/repositories/snapshots'
    }
    return 'https://oss.sonatype.org/service/local/staging/deploy/maven2'
}

subprojects {

    ext {
        guavaVersion = '30.1.1-jre'
    }

    apply plugin: 'java-library'
    apply plugin: 'maven-publish'
    apply plugin: 'signing'

    sourceCompatibility = '1.8'
    targetCompatibility = '1.8'

    repositories {
        mavenCentral()
    }

    dependencies {
        compileOnly 'javax.annotation:jsr250-api:1.0'
        compileOnly 'net.jcip:jcip-annotations:1.0'
        compileOnly 'com.github.spotbugs:spotbugs-annotations:4.2.0'
    }

    group = 'io.contek.ursa'
    version = '1.1.1-SNAPSHOT'

    task sourcesJar(type: Jar) {
        from sourceSets.main.allSource
        archiveClassifier.set('sources')
    }

    task javadocJar(type: Jar) {
        from javadoc
        archiveClassifier.set('javadoc')
    }

    publishing {
        publications {
            mavenJava(MavenPublication) {
                artifactId = "ursa-${project.name}"
                from components.java
                artifact sourcesJar
                artifact javadocJar

                pom {
                    afterEvaluate {
                        name = project.description
                        description = project.description
                    }

                    url = 'https://github.com/contek-io/io.contek.ursa'

                    scm {
                        connection = 'scm:git:git://github.com/contek-io/io.contek.ursa.git'
                        developerConnection = 'scm:git:ssh://github.com/contek-io/io.contek.ursa.git'
                        url = 'http://github.com/contek-io/io.contek.ursa/tree/master'
                    }

                    licenses {
                        license {
                            name = 'MIT License'
                            url = 'https://opensource.org/licenses/MIT'
                        }
                    }

                    developers {
                        developer {
                            id = 'contek'
                            name = 'Dev Contek'
                            email = 'dev@contek.io'
                            organization = 'Contek'
                            organizationUrl = 'https://contek.io'
                        }
                    }
                }
            }
        }
        repositories {
            maven {
                url = getRepositoryUrl(version)
                credentials {
                    username = project.hasProperty('sonatypeUsername') ? sonatypeUsername : ''
                    password = project.hasProperty('sonatypePassword') ? sonatypePassword : ''
                }
            }
        }
    }

    signing {
        required { !isSnapshot(version) }
        sign publishing.publications.mavenJava
    }
}
